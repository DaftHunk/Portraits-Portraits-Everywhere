BACKUP ~PPE/backup~
AUTHOR ~Smeagolheart&bob_veng@forum.baldursgate.com~
VERSION ~0.91~
LANGUAGE ~English~
	~en_US~
	~PPE/lang/en_US/setup.tra~

BEGIN @10000
	REQUIRE_PREDICATE GAME_IS ~bgee bg2ee iwdee eet sod~ @10010
	DESIGNATED 0

	DEFINE_ACTION_MACRO ~Addpic~ BEGIN
			ACTION_IF FILE_EXISTS ~%MOD_FOLDER%/portraits/%Filename%.bmp~ THEN BEGIN
				LOG ~PATCHING: %Crename%.cre with PORTRAIT: %Filename%.bmp~
				COPY_EXISTING ~%Crename%.cre~ ~override~
				WRITE_EVALUATED_ASCII 0x34 ~%Filename%~ #8
				ACTION_IF NOT FILE_EXISTS ~override\%Filename%.bmp~ THEN BEGIN
					COPY ~%MOD_FOLDER%/portraits/%Filename%.bmp~ ~override~
				END
			END	ELSE BEGIN     // If portrait's not in the mod, just update reference (Re-use existing portraits)
				COPY_EXISTING ~%Crename%.cre~ ~override~
				WRITE_EVALUATED_ASCII 0x34 ~%Filename%~ #8
			END
	END
	
	PRINT @10001
	OUTER_SET s9total=0

	// BG1 Specific Actions
	ACTION_IF GAME_IS ~bgee eet~ THEN BEGIN		
		COPY ~%MOD_FOLDER%/copy/BG1~ ~override~
		// Portrait Swapping scripts
		COMPILE ~%MOD_FOLDER%/dialogue/thalan.d~
		COMPILE ~%MOD_FOLDER%/dialogue/dopdur1.d~
		COMPILE ~%MOD_FOLDER%/dialogue/dopdur2.d~
		COMPILE ~%MOD_FOLDER%/dialogue/dopdur3.d~
		COMPILE ~%MOD_FOLDER%/dialogue/elmins4.d~
		//COMPILE ~%MOD_FOLDER%/dialogue/rashad.d~
	END
	// BG2 Specific Actions
	// including overwriting the Low Res / Quality Default BG2 Portraits for Bhaalspawn
	ACTION_IF GAME_IS ~bg2ee eet~ THEN BEGIN
		COPY ~%MOD_FOLDER%/copy/BG2~ ~override~
		// Portrait Swapping scripts
		COMPILE ~%MOD_FOLDER%/dialogue/shape.d~
	END
	
	OUTER_SET rows_cnt_1=0
	COPY - ~%MOD_FOLDER%/tbl/NPCPortraits.tbl~  ~%MOD_FOLDER%/tbl/NPCPortraits.tbl~
		COUNT_2DA_ROWS 1 "rows_cnt_1"
			FOR( cntg=0; cntg<"%rows_cnt_1%"; cntg+=1 ) BEGIN
				READ_2DA_ENTRY cntg  0 2 "Crename"
				READ_2DA_ENTRY cntg  1 2 "Filename"
				INNER_ACTION BEGIN
					ACTION_IF FILE_EXISTS_IN_GAME ~%Crename%.cre~ THEN BEGIN
						OUTER_SET s9total+=1
						LAUNCH_ACTION_MACRO ~Addpic~ 
					END
				END
			END
	//Count them up :)
	PRINT ~-------------------------------------------------------------~
	PRINT @10002
	PRINT ~-------------------------------------------------------------~

BEGIN @20000
	REQUIRE_PREDICATE GAME_IS ~bgee bg2ee iwdee eet sod~ @1001010
	REQUIRE_COMPONENT ~PPE/SETUP-PPE.TP2~ ~0~ @10999
	DESIGNATED 100
	SUBCOMPONENT @13000	
	OUTER_SET IsRnd 		= 0		// Not Random
	INCLUDE ~%MOD_FOLDER%/lib/S9MP-CLib.tph~
	
BEGIN @20050
	REQUIRE_PREDICATE GAME_IS ~bgee bg2ee iwdee eet sod~ @10010
	REQUIRE_COMPONENT ~PPE/SETUP-PPE.TP2~ ~0~ @10999
	DESIGNATED 150
	SUBCOMPONENT @13000
	OUTER_SET IsRnd 		= 1		// Random
	INCLUDE ~%MOD_FOLDER%/lib/S9MP-CLib.tph~
	